#name			:valideren van disjoint individuals via Disjoint verklaringen op basis van owl:allDisjoint
#description		:Disjoint via owl:allDisjointClasses 
#reference		:Cax-adx (listmod)
#profile			:COINS 2.0 Lite

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

select distinct ?x ?c1 ?c2

WHERE {
  Graph <schemaGraph> {
   ?y a owl:AllDisjointClasses .
   ?y owl:members ?members .
   ?members rdf:rest*/rdf:first ?c1 .
   ?members rdf:rest*/rdf:first ?c2 .
   FILTER (?c1 != ?c2) .
 }
 Graph <instanceGraph> {
  ?x a ?c1 .
  ?x a ?c2
 }
}